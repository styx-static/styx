== Basics

Styx uses the Nix expression language.
The Nix expression language is a lazily evaluated functional language with uncommon semantics. +
It is recommended to read the link:http://nixos.org/nix/manual/#ch-expression-language[Nix expression language chapter] of the Nix manual to get more familiar with it.

=== Hello world!

Let's look at a very basic example of a styx site `site.nix` file.

[source, nix]
.Hello world!
----
{ styx, ... }@args: # <1>

rec { # <2>

  lib = import styx.lib styx; # <3>

  index = { # <4>
    layout   = template: "<html><body>${template}</body></html>"; # <5>
    template = page: '' # <6>
      <h1>Styx example page</h1>
      ${page.content}
    '';
    content = "<p>Hello world!</p>"; # <7>
    path    = "/index.html"; # <8>
  };

  site = lib.mkSite { pageList = [ index ]; }; # <9>

}
----

<1> `site.nix` is a function written in the nix language. Nix functions have the `{ arg1 ? default, arg2, ..., argN, ... }: body` pattern. +
The arguments of the function are the dependencies needed to build the site. +
`@args` refers to the whole argument set.
<2> `{` starts a set. `rec` makes the set recursive, meaning that attributes can refer to each other.
<3> This imports the styx library in a `lib` attribute.
<4> This declares an `index` attribute as an attribute set. An attribute set is a basic key-value structure. `index` will be the only page generated.
<5> To generate a page, styx requires a few attributes to be set, namely `layout`, `template` and `path`. This line declares the `layout` attribute that is a function that takes the `template` argument and returns the page source. +
`template:` form is different from the first point function head, this function use a simple parameter (Point one was using a deconstructed attribute set parameter).
<6> This declares a `template` attribute. `template` is a function that takes the page attribute set, `page` (in this case as a parameter) and generates a result that will be passed to the `layout` function.
<7> This declares a `content` attribute. This is not mandatory, and could have been integrated in the `template` key. But most of styx data fetchers will add a `content` attribute to the page set containing the fetched content.
<8> This declares a `path` attribute. The `path` attribute is used to specify the path of the page to be generated.
<9> This generates the site by using the library `mkSite` function, its parameter is an attribute set with the argument `pageList` being a list of the single `index` page.

Everything is ready, the "Hello world!" site preview can be generated.


[source, sh]
.Generating a preview of the "Hello world!" site
----
$ styx preview
----


TIP: Even if this is a simple example, nix language semantics can be confusing. +
In a nutshell, to build a site, `mkSite` evaluates `page.layout (page.template page)` and output the result in `page.path` for every page in `pageList`. +
That is the reason `layout`, `template` and `path` are required attributes of a page set.

NOTE: Nix expression language is lazily evaluated, so infinite data structures are not a problem (unless some code tries to evaluate them).

=== Files

In its simplest form, styx just needs a `site.nix` to generate a site like presented in the <<Hello world!>> example.

But the `styx new` command will generate a few more files for convenience.

[source]
----
├── conf.nix    # <1>
├── default.nix # <2>
├── readme.md   # <3>
├── site.nix    # <4>
├── data/       # <5>
└── themes/     # <6>
----

<1> `conf.nix` is the main configuration file, see <<Configuration>> for details.
<2> `default.nix` is a file that can be used to build a styx file directly with `nix-build`.
<3> `readme.md` provides some basic information about how to start using styx.
<4> `site.nix` is the main file for site generation, see <<site.nix,site.nix>> for details.
<5> `data/` is an empty directory meant to hold site data.
<6> `themes/` is an empty directory meant to hold custom themes.

NOTE: The file structure of a styx site is totally free and can be changed according to needs. +
It is even possible to change the name of `site.nix`. In that case the `--file` flag of the styx command can be used to specify the file name.


[[site.nix]]
=== site.nix

The `site.nix` used in the <<Hello world!>> example contains only enough code to generate a single page. A standard `site.nix` is usually more structured.

A complex example of a `site.nix` if the showcase theme example `site.nix` that uses almost every feature available.

A standard `site.nix` is divided in multiple sections, each section handling a particular topic. +
Standard sections are:

- <<site.nix-init,Init>>
- <<site.nix-themes,Themes>>
- <<site.nix-data,Data>>
- <<site.nix-pages,Pages>>
- <<site.nix-site,Site>>

[[site.nix-init]]
==== Init

This section is the basic setup of styx, it should not be changed and used as is for most setups.

[source, nix]
.Standard Init section
----
/*-----------------------------------------------------------------------------
   Init

   Initialization of Styx, should not be edited
-----------------------------------------------------------------------------*/

{ styx
, extraConf ? {}
}@args:

rec {

  /* Styx library
  */
  styxLib = import styx.lib styx; <1>
----

<1> Load the styx library, library functions are documented in link:./library.html[library documentation].


[[site.nix-themes]]
==== Themes

The theme section is responsible for loading themes assets (configuration, library, static files, and templates).

Themes are detailed in the <<Themes>> section.

[source, nix]
.Standard themes section
----
/*-----------------------------------------------------------------------------
   Theme setup

-----------------------------------------------------------------------------*/

  /* Importing styx themes from styx
  */
  styx-themes = import styx.themes; # <1>

  /* list the themes to load, paths or packages can be used
     items at the end of the list have higher priority
  */
  themes = [ # <2>
    styx-themes.generic-templates
  ];

  /* Load the theme data
  */
  themeData = styxLib.themes.load {
    inherit styxLib themes;
    extraEnv = { inherit data pages; }; # <3>
    extraConf = [ ./conf.nix extraConf ]; # <4>
  };

  /* Bring the theme data into scope
  */
  inherit (themeData) conf lib files templates env; # <5>
----

<1> Importing the styx-themes.
<2> `themes` is a list so it is possible to set multiple themes at the same time to <<Combining Themes,combine them>>. Themes at the beginning of the list have a lower priority. +
Themes can be paths like `./themes/my-site` or packages from the `styx-themes` set.
<3> Extra variables to add to the template environment.
<4> Extra configuration sets to merge with the themes configurations, can be files or paths, head of the list have lower priority.
<5> Bringing the `themeData` attributes in the scope.

[[site.nix-data]]
==== Data

The data section is responsible for loading the data used in the site.

The <<Data>> section explains in detail how to manage data.

[source, nix]
.Standard data section
----
/*-----------------------------------------------------------------------------
   Data

   This section declares the data used by the site
-----------------------------------------------------------------------------*/

  data = {
    about = lib.loadFile { file = ./pages/about.md; inherit env; }; # <1>
  };
----

<1> Example of loading a Markdown file with the `loadFile` function.

[[site.nix-pages]]
==== Pages

The pages section is used to declare the pages that will be generated by `mkSite`. +
Even if `mkSite` expects a page list, it is usually declared as an attribute set for convenience.

There are multiple functions available to generate different type of pages, but a page is ultimately an attribute set with at least the `layout`, `template` and `path` attribute defined.

The <<Pages>> section explains in detail how to create pages.

[source, nix]
.Standard pages section
----
/*-----------------------------------------------------------------------------
   Pages

   This section declares the pages that will be generated
-----------------------------------------------------------------------------*/

  pages = {

    about = {
      path     = "/about.html";
      template = templates.page.full;
      layout   = templates.layout;
    } // data.about; # <1>

  };
----

<1> `//` is the operator to merge attribute sets, this merge the `data.about` data attribute set in the `pages.about` page attribute set.

IMPORTANT: `path` must start with a `/`.

[[site.nix-site]]
==== Site

This is the final part and shortest section of `site.nix`. This section consists of a call to link:library.html#lib.generation.mkSite[`mkSite`].

[source, nix]
.Standard mkSite section
----
/*-----------------------------------------------------------------------------
   Site

-----------------------------------------------------------------------------*/

  /* Converting the pages attribute set to a list
  */
  pageList = lib.pagesToList { inherit pages; }; # <1>

  site = lib.mkSite { inherit files pageList; }
}
----

<1> `mkSite` requires pages as a list, so `pagesToList` converts the pages attribute set to a list.

NOTE: `files` is generated in the theme section using enabled themes. +
`inherit` is a shorthand for writing sets, `{ inherit a; }` is equivalent to `{ a = a; }`.

==== site.nix in a nutshell

====
`site.nix` is a function:

- taking at least nixpkgs `lib`, `styx`, `runCommand` and `writetext` attributes.
- returning an attribute set with a `site` attribute using the `mkSite` function.
====

====
`mkSite` is a function:

- taking at least a list of pages to generate as the `pageList` argument.
- evaluating each page set by evaluating `page.layout (page.template page)` and outputting the result in `page.path`.
- returning a generated static site directory.

NOTE: `mkSite` is a wrapper for nixpkgs `runCommand` function.
====

