== Basics

Styx is using the Nix expression language.
The Nix expression language is a lazy evaluated functional language with unconventional semantics. +
It is recommended to read the link:http://nixos.org/nix/manual/#ch-expression-language[Nix expression language chapter] of the Nix manual to get more familiar with it.

=== Hello world!

Let's look at a very basic example of a styx site.

[source, nix]
.Hello world!
----
{ lib, styx, runCommand, writeText, ... }@args: # <1>

rec { # <2>

  lib = import styx.lib args; # <3>

  index = { # <4>
    layout = template: "<html><body>${template}</body></html>"; # <5>
    template = page: '' # <6>
      <h1>Styx example page</h1>
      ${page.content}
    '';
    content = "<p>Hello world!</p>"; # <7>
    href = "index.html"; # <8>
  };

  site = lib.generateSite { pagesList = [ index ]; }; # <9>

}
----

<1> `site.nix` is a function written in the nix language. Nix functions have the `{ arg1 ? default, arg2, ..., argN, ... }: body` pattern. +
The arguments of the function are the dependencies needed to build the site. +
`@args` refers to the whole argument set.
<2> `{` is starting a set. `rec` makes the set recursive, meaning that attributes can refers each other.
<3> This imports the styx library in a `lib` attribute.
<4> This declare an `index` attribute as an attribute set. An attribute set is a basic key-value structure. `index` will be the only page generated.
<5> To generate a page, styx requires a few attributes to be set, namely `layout`, `template` and `href`. This line declares the `layout` attribute that is a function that take the `template` argument and return the page source. +
`template:` form is different from the first point function head, this function use a simple parameter (Point one was using a deconstructed attribute set parameter).
<6> This declare a `template` attribute. `template` is a function that takes the page attribute set, `page` in this case as a parameter and generate a result that will be passed to the `layout` function.
<7> This declare a `content` attribute. This is not mandatory, and could have been integrated in the `template` key. But most of styx data fetchers will add a `content` attribute to the page set containing the fetched content.
<8> This declare a `href` attribute. The `href` attribute is used to specify the path of the page to be generated.
<9> This generate the site by using the library `generateSite` function, its parameter is an attribute set with the argument `pagesList` being a list of the single `index` page.

Everything is ready, the "Hello world!" site preview can be generated.

[source, shell]
.Generating a preview of the "Hello world!" site
----
$ styx preview
----

TIP: Even if this is a simple example, nix language semantics can be confusing. In a nutshell to build a site, `generateSite` evaluates `page.layout (page.template page)` and output the result in `page.href` for every page in `pagesList`. +
That is the reason `layout`, `template` and `href` are required attributes of a page set.

NOTE: Nix expression language is lazily evaluated, so infinite data structures are not a problem (unless some code try to evaluate them).

=== Structure

In its simplest form, styx just needs a `site.nix` to generate a site like presented in the <<Hello world!>> example. 

But the `styx new` command will generate a few more files for convenience.

[source]
----
├── conf.nix # <1>
├── readme.md # <2>
├── site.nix # <3>
├── data/ # <4>
└── themes/ # <5>
----

<1> `conf.nix` is the main configuration, see <<Configuration>> for details.
<2> `readme.md` provides some basic information about how to start using styx.
<3> `site.nix` is the main file for site generation, see <<sitenix>> for details.
<4> `data/` is an empty directory meant to hold site data.
<5> `themes/` is an empty directory meant to hold custom themes.

NOTE: The file structure of styx is totally free and can be changed at will. +
It is even possible to change the name of `site.nix`. In that case the `--file` flag of the styx command can be used to specify the file name.


=== site.nix

The `site.nix` used in the <<Hello world!>> example contains only enough code to generate a single page. It is likely that a `site.nix` to generate a normal site will contain more informations.

The extreme case is the `showcase` theme example site `site.nix` that use almost every feature available.

It is best practice to divide `site.nix` in multiple sections, with each section handling a particular topic. +
It is common to use the following sections:

- Init
- Themes
- Data
- Pages
- Site

==== site.nix in a nutshell

====
`site.nix` is a function:

- taking at least nixpkgs `lib`, `styx`, `runCommand` and `writetext` attributes.
- returning an attribute set with a `site` attribute using the `generateSite` function.
====

====
`generateSite` is a function:

- taking at least the list of pages set to generate as an argument.
- that evaluate each page set with `page.layout (page.template page)` and output the result in `page.href`.
- returning a generated static site.

NOTE: `generateSite` is a wrapper for nixpkgs `runCommand` function.
====

==== Init

This section is the basic setup of styx, it should not be changed and used as is for most setups.

[source, nix]
.Standard Init section
----
/*-----------------------------------------------------------------------------
   Init

   Initialization of Styx, should not be edited
-----------------------------------------------------------------------------*/

{ lib, styx, runCommand, writeText
, styx-themes
, extraConf ? {}
}@args:

rec {

  lib = import styx.lib args;

  /* Configuration loading
  */
  conf = lib.utils.loadConf { # <1>
    file = ./conf.nix;
    inherit themes extraConf;
  };

  /* Themes templates loading
  */
  templates = lib.themes.loadTemplates { # <2>
    inherit themes;
    environment = { inherit conf templates data pages lib; };
  };

  /* Themes static files loading
  */
  files = lib.themes.loadFiles themes; # <3>
----

<1> This loads the `conf.nix` file and merges it with theme configuration and main function `siteUrl` argument.
<2> This loads the templates provided by the themes.
<3> This loads the static files loaded by the themes.


[[site.nix-themes]]
==== Themes

This section is where used themes are declared. Themes are a central concept in styx and provide ways to manage site assets in a flexible manner.

Themes are detailed in the <<Themes>> section.

[source, nix]
.Standard themes section
----
/*-----------------------------------------------------------------------------
   Themes setup

-----------------------------------------------------------------------------*/

  /* Themes used
  */
  themes = [ styx-themes.showcase ]; # <1>
----

<1> `themes` is a list so it is possible to set multiple themes at the same time to combine them. Themes at the beginning of the list have a higher priority. +
Themes can be paths like `./themes/my-site` or packages from the `styx-themes` set.


==== Data

The data section is responsible for loading the data used in the site.

The <<Data>> section explains in detail how to manage data.

[source, nix]
.Standard data section 
----
/*-----------------------------------------------------------------------------
   Data

   This section declares the data used by the site
-----------------------------------------------------------------------------*/

  data = {
    about = loadFile { dir = ./pages; file = "about.md"; }; # <1>
  };
----

<1> Example of loading a markdown file with the `loadFile` function.

==== Pages

The pages section is used to declare the pages that will be generated by `generateSite`. +
Even if `generateSite` expects a page list, it is usually declared as an attribute set for convenience.

There are multiple functions available to generate different type of pages, but a page is ultimately an attribute set with at least the `layout`, `template` and `href` attribute defined.

The <<Pages>> section explains in detail how to create pages.

[source, nix]
.Standard pages section
----
/*-----------------------------------------------------------------------------
   Pages

   This section declares the pages that will be generated
-----------------------------------------------------------------------------*/

  pages = {

    about = {
      href = "about.html";
      template = templates.generic.full;
    } // data.about; # <1>

  };
----

<1> `//` is the operator to merge attribute sets, this merge the `data.about` data attribute set in the `pages.about` page attribute set.

NOTE: As many pages tends to use the same layout, the `layout` attribute is usually set in one go to all pages in the site section.
Only pages that use a different layout usually explicitly set it in `pages`.

==== Site

This is the final part and shortest section of `site.nix`. This section consists in a call to <<lib.generation.generateSite,`generateSite`>>.

[source, nix]
.Standard generateSite section
----
/*-----------------------------------------------------------------------------
   Site

-----------------------------------------------------------------------------*/

  /* Converting the pages attribute set to a list
  */
  pagesList = pagesToList pages; # <1>

  site = generateSite { inherit files pagesList; }
}
----

<1> `generateSite` requires pages as a list, so `pagesToList` convert the pages attribute set to a list.

NOTE: `files` is automatically generated in the init section using enabled themes.

NOTE: `inherit` is a shorthand for writing sets, `{ inherit a; }` is equivalent to `{ a = a; }`.


=== Configuration

Styx is configured with the `conf.nix` file present in the site root directory.

This files consists in an attribute set defining configuration options, and custom attributes can be added at will.

The main configuration is made through themes via the `theme` attribute. Every theme defines some set of configuration options that can be overridden in `conf.nix` `theme` attribute.

`siteUrl` is the only required field, and must not end with a slash.

[source, nix]
.Standard conf.nix
----
{
  # URL of the site, must be set to the url of the domain the site will be deployed
  siteUrl = "http://yourdomain.com";

  # Theme specific settings
  # it is possible to override any of the theme configuration settings in the 'theme' set
  theme = {
    # Changing the theme site.title setting
    site.title = "Styx Site";
  };
}
----

