== Basics

Styx is using the Nix expression language.
The Nix expression language is a lazy evaluated functional language with unconventional semantics. +
It is recommended to read the link:http://nixos.org/nix/manual/#ch-expression-language[Nix expression language chapter] of the Nix manual to get more familiar with it.

=== Hello world!

Let's look at a very basic example of a styx site.

[source, nix]
.Hello world!
----
{ lib, styx, runCommand, writeText, ... }@args: # <1>

rec { # <2>

  lib = import styx.lib args; # <3>

  index = { # <4>
    layout   = template: "<html><body>${template}</body></html>"; # <5>
    template = page: '' # <6>
      <h1>Styx example page</h1>
      ${page.content}
    '';
    content = "<p>Hello world!</p>"; # <7>
    path    = "/index.html"; # <8>
  };

  site = lib.generateSite { pagesList = [ index ]; }; # <9>

}
----

NOTE: This example does not use themes.

<1> `site.nix` is a function written in the nix language. Nix functions have the `{ arg1 ? default, arg2, ..., argN, ... }: body` pattern. +
The arguments of the function are the dependencies needed to build the site. +
`@args` refers to the whole argument set.
<2> `{` is starting a set. `rec` makes the set recursive, meaning that attributes can refers each other.
<3> This imports the styx library in a `lib` attribute.
<4> This declare an `index` attribute as an attribute set. An attribute set is a basic key-value structure. `index` will be the only page generated.
<5> To generate a page, styx requires a few attributes to be set, namely `layout`, `template` and `path`. This line declares the `layout` attribute that is a function that take the `template` argument and return the page source. +
`template:` form is different from the first point function head, this function use a simple parameter (Point one was using a deconstructed attribute set parameter).
<6> This declare a `template` attribute. `template` is a function that takes the page attribute set, `page` in this case as a parameter and generate a result that will be passed to the `layout` function.
<7> This declare a `content` attribute. This is not mandatory, and could have been integrated in the `template` key. But most of styx data fetchers will add a `content` attribute to the page set containing the fetched content.
<8> This declare a `path` attribute. The `path` attribute is used to specify the path of the page to be generated.
<9> This generate the site by using the library `generateSite` function, its parameter is an attribute set with the argument `pagesList` being a list of the single `index` page.

Everything is ready, the "Hello world!" site preview can be generated.

[source, shell]
.Generating a preview of the "Hello world!" site
----
$ styx preview
----

TIP: Even if this is a simple example, nix language semantics can be confusing. +
In a nutshell to build a site, `generateSite` evaluates `page.layout (page.template page)` and output the result in `page.path` for every page in `pagesList`. +
That is the reason `layout`, `template` and `path` are required attributes of a page set.

NOTE: Nix expression language is lazily evaluated, so infinite data structures are not a problem (unless some code try to evaluate them).

=== Structure

In its simplest form, styx just needs a `site.nix` to generate a site like presented in the <<Hello world!>> example. 

But the `styx new` command will generate a few more files for convenience.

[source]
----
├── conf.nix # <1>
├── readme.md # <2>
├── site.nix # <3>
├── data/ # <4>
└── themes/ # <5>
----

<1> `conf.nix` is the main configuration file, see <<Configuration>> for details.
<2> `readme.md` provides some basic information about how to start using styx.
<3> `site.nix` is the main file for site generation, see <<site.nix,site.nix>> for details.
<4> `data/` is an empty directory meant to hold site data.
<5> `themes/` is an empty directory meant to hold custom themes.

NOTE: The file structure of styx is totally free and can be changed according to needs. +
It is even possible to change the name of `site.nix`. In that case the `--file` flag of the styx command can be used to specify the file name.


[[site.nix]]
=== site.nix

The `site.nix` used in the <<Hello world!>> example contains only enough code to generate a single page. A standard `site.nix` will contain more information and be more structured.

A complex example of a `site.nix` if the showcase theme example site link:https://github.com/styx-static/styx-theme-showcase/blob/master/example/site.nix[`site.nix`] that use almost every feature available.

It is best practice to divide `site.nix` in multiple sections, with each section handling a particular topic. +
It is common to use the following sections:

- Init
- Themes
- Data
- Pages
- Site

==== site.nix in a nutshell

====
`site.nix` is a function:

- taking at least nixpkgs `lib`, `styx`, `runCommand` and `writetext` attributes.
- returning an attribute set with a `site` attribute using the `generateSite` function.
====

====
`generateSite` is a function:

- taking at least a list of pages to generate as the `pagesList` argument.
- that evaluate each page set by evaluating `page.layout (page.template page)` and output the result in `page.path`.
- returning a generated static site directory.

NOTE: `generateSite` is a wrapper for nixpkgs `runCommand` function.
====

==== Init

This section is the basic setup of styx, it should not be changed and used as is for most setups.

[source, nix]
.Standard Init section
----
/*-----------------------------------------------------------------------------
   Init

   Initialization of Styx, should not be edited
-----------------------------------------------------------------------------*/

{ lib, styx, runCommand, writeText
, styx-themes
, extraConf ? {}
}@args:

rec {

  /* Styx library
  */
  styxLib = import styx.lib args; <1>
----

<1> Load the styx library, library functions are documented in <<Library>>.


[[site.nix-themes]]
==== Themes

The theme section is responsible for loading themes assets (configuration, library, static files, and templates).

Themes are detailed in the <<Themes>> section.

[source, nix]
.Standard themes section
----
/*-----------------------------------------------------------------------------
   Themes setup

-----------------------------------------------------------------------------*/

  /* list the themes to load, paths or packages can be used
     items at the end of the list have higher priority
  */
  themes = [ # <1>
    styx-themes.showcase
  ];

  /* Loading the themes data
  */
  themesData = styxLib.themes.load {
    inherit styxLib themes;
    templates.extraEnv = { inherit data pages; }; # <2>
    conf.extra = [ (import ./conf.nix) extraConf ]; # <3>
  };

  /* Bringing the themes data to the scope
  */
  inherit (themesData) conf lib files templates; # <4>
----

<1> `themes` is a list so it is possible to set multiple themes at the same time to combine them. Themes at the beginning of the list have a lower priority. +
Themes can be paths like `./themes/my-site` or packages from the `styx-themes` set.
<2> Extra variables to add to the template environment.
<3> Extra configuration sets to merge with the themes configurations, head of the list have lower priority.
<4> Bringing the `themesData` attributes in the scope.

==== Data

The data section is responsible for loading the data used in the site.

The <<Data>> section explains in detail how to manage data.

[source, nix]
.Standard data section 
----
/*-----------------------------------------------------------------------------
   Data

   This section declares the data used by the site
-----------------------------------------------------------------------------*/

  data = {
    about = loadFile { dir = ./pages; file = "about.md"; }; # <1>
  };
----

<1> Example of loading a markdown file with the `loadFile` function.

==== Pages

The pages section is used to declare the pages that will be generated by `generateSite`. +
Even if `generateSite` expects a page list, it is usually declared as an attribute set for convenience.

There are multiple functions available to generate different type of pages, but a page is ultimately an attribute set with at least the `layout`, `template` and `path` attribute defined.

The <<Pages>> section explains in detail how to create pages.

[source, nix]
.Standard pages section
----
/*-----------------------------------------------------------------------------
   Pages

   This section declares the pages that will be generated
-----------------------------------------------------------------------------*/

  pages = {

    about = {
      path     = "/about.html";
      template = templates.generic.full;
      layout   = templates.layout;
    } // data.about; # <1>

  };
----

<1> `//` is the operator to merge attribute sets, this merge the `data.about` data attribute set in the `pages.about` page attribute set.


==== Site

This is the final part and shortest section of `site.nix`. This section consists in a call to <<lib.generation.generateSite,`generateSite`>>.

[source, nix]
.Standard generateSite section
----
/*-----------------------------------------------------------------------------
   Site

-----------------------------------------------------------------------------*/

  /* Converting the pages attribute set to a list
  */
  pagesList = pagesToList { inherit pages; }; # <1>

  site = generateSite { inherit conf files pagesList; }
}
----

<1> `generateSite` requires pages as a list, so `pagesToList` convert the pages attribute set to a list.

NOTE: `files` is generated in the init section using enabled themes.

NOTE: `inherit` is a shorthand for writing sets, `{ inherit a; }` is equivalent to `{ a = a; }`.

