[[Themes]]
== Themes

Themes are a central concept in styx and are used to manage a site assets, namely files and templates.
They also provide a configuration interface and can declare a function library.

Themes can be combined in a modular way.

=== Getting themes

Styx themes are listed in the official link:https://github.com/styx-static/themes[theme repository].

Themes available at the moment of writing this documentation are:

- link:https://github.com/styx-static/styx-theme-showcase[Showcase]: A theme that show most of styx functionalities and provides many templates.
- link:https://github.com/styx-static/styx-theme-hyde[Hyde]: A port of the link:https://github.com/poole/hyde[Jekyll Hyde theme] to styx. Hyde is a simple and beautiful blog theme.
- link:https://github.com/styx-static/styx-theme-agency[Agency]: A port of the link:http://themes.gohugo.io/agency/[Hugo agency theme] to styx. Agency is a single page site with a modern design fitting company or agency websites.
- link:https://github.com/styx-static/styx-theme-orbit[Orbit]: A port of the link:http://themes.gohugo.io/orbit/[Hugo orbit theme] to styx. Orbit is a single page site resume / profile theme.

=== Using themes

Declaring used themes is done in the <<site.nix-themes,theme section>> of `site.nix`.
Official themes can be used directly from the `styx-themes` package set.

[source, nix]
.Using a local my-site theme and the showcase theme
----
  /* list the themes to load, paths or packages can be used
     items at the end of the list have higher priority
  */
  themes = [
    styx-themes.showcase
    ./themes/my-theme
  ];
----

NOTE: Items at the beginning of the `themes` list have lower priority.

[[Themes.Structure]]
=== Structure

Themes are usually stored in the `themes` directory, or used from the `styx-themes` package set.

Every theme is stored in its own directory, and themes are expected to follow a certain structure.

[source, shell]
.Structure of a Styx theme
-----
themes
└── NAME # <1>
    ├── conf.nix # <2>
    ├── meta.nix # <3>
    ├── lib.nix # <4>
    ├── files # <5>
    └── templates # <6>
-----

<1> Name of the theme. The name should be a valid directory name.
<2> `conf.nix` defines the theme configuration options (optional). 
<3> `meta.nix` defines the theme meta information, this is file is required and must define a `name` attribute.
<4> `lib.nix` defines the theme extra library as a set (optional).
<5> `files` is a directory holding static files for the themes. All the files in this directory will be copied in the generated site (optional).
<6> `templates` is a directory storing all the templates files (optional).


=== conf.nix

`conf.nix` defines the theme configuration interface and is defined as an attribute set.

It is a simple attribute set that defines the theme configuration options with some default values.

[source, nix]
.Example of a theme conf.nix
----
{
  site = {
    title = "The Agency";
    author = "Your name";
    description = "Your description";
  };
}
----

====
`conf.nix` attribute set will be merged in `site.nix` by the `lib.themes.load` function into the `conf` variable containing two attributes:

- `conf.theme`: The global theme configuration, all the enabled themes configuration will be merged in this variable. This allow to extend theme configuration in `conf.nix`. +
Theme templates that access themes configuration should use `conf.theme`.
- `conf.themes.NAME`: `NAME` is the theme name, this holds the theme default configuration as it is declared in `theme.nix`.
====

NOTE: `conf.nix` can be an attribute set or a function, if it is a function it will be called with an attribute set argument containing `lib`.

=== meta.nix

Theme meta information should be declared in the `meta.nix` file as an attribute set. Themes *must* define a `name` attribute in `meta.nix` so the theme configuration is correctly processed. +

Other attributes of the `meta` set are totally optional and can be set at will to provide information about the theme (e.g. `license`, `description`, `author`, `homepage`, ...).

[source, nix]
.Example of a theme meta.nix
----
{ lib }:
{
  name = "showcase";
  license = lib.licenses.mit;
  maintainers = with lib.maintainers; [ ericsagnes ];
}
----

NOTE: `meta.nix` can be an attribute set or a function, if it is a function it will be called with an attribute set argument containing `lib`.

=== Files

The `files` directory holds the static files provided by the theme. Every file in in this directory will be copied in the generated site.

NOTE: If multiple themes are enabled, every theme `file` directory contents will be copied to the generated site. If this is not desirable, it is possible to pass a different list of themes to <<lib.themes.load, lib.themes.load>> function `files` argument.

==== Special files

Styx automatically convert `sass`, `scss` and `less` files to css during site generation.

[[Themes.Library]]
=== Library

Themes can provide a function library that can be used in `site.nix` or in templates.

To provide a library, a theme must have a `lib.nix` file containing an attribute set of functions.

To be sure a theme library functions will not override default functions it is best practice to put the functions in a dedicated namespace, `theme.NAME` where `NAME` is the theme name as a natural choice.

[source, nix]
.Example of a theme lib.nix
----
{ lib }:
{
  theme.my-theme = {
    foo = x: "foo called with ${builtins.toString x}";
  };
}
----

NOTE: `lib.nix` can be an attribute set or a function, if it is a function it will be called with an attribute set argument containing `lib`.


=== Templates

Every template in the `templates` directory will be accessible by its file basename in the `templates` attribute set.
Directories are converted to nested sets.

[source, shell]
.Example of a template directory structure
-----
├── archive.nix
└── post
    ├── full.nix
    └── list.nix
-----

[source, nix]
.Generated attribute set
-----
templates = {
  archive = <<lambda>>;
  posts = {
    full = <<lambda>>;
    list = <<lambda>>;
  };
};
-----

NOTE: the `\<<lambda>>` notation is borrowed from the `nix-repl` tool, and means a non fully evaluated function. +
The nix language is functional, and it allows to partially apply functions and to pass function as argument to other functions.

'''

Styx templates are functions that return text or page attribute sets when fully evaluated.

Templates are divided in three types:

- *Layout templates*, responsible for the final rendering of a page, it must return text. (page attribute set `layout` key)
- *Normal templates*, responsible for preparing a page for the layout template, this kind of templates usually return a page attribute set.
- *Partial templates*, responsible for rendering a part of a layout or a normal template. 

The template evaluation flow is:

- *Normal templates* evaluate the page attribute set and generates a partial result that is stored in the page attribute set `content` key.
- *Layout templates* evaluate the page attribute set returned by the *normal template* and produce the final source of the page.

==== Text handling basics

Most of the work in template is done by manipulating text.

This section introduce the basics of text handling in the templates:

- single-line or multi-line text (leading spaces are stripped without changing relative line align), delimited by `''`.

+
[source, nix]
.Multi-line text
----
''
  Hello world!
  Hello Styx!
''
----

- single-line or multi-line text, delimited by `"`.

+
[source, nix]
.Single-line text
----
"Hello world!"
----

Nix expressions can be included in text in enclosed in `${...}`.

[source, nix]
.Expression antiquotation
----
let name = "world"; in
"Hello ${name}!"
----

==== Layout templates

Layout templates are responsible for rendering the final source of a page. +
A layout template function usually takes a template environment, and a page attribute set with a `content` attribute.

In a HTML context, the layout template is usually responsible of the HTML code that is not directly related to the content, like the `html` tag contents.

[source, nix]
.Layout template example
----
env:
page:
''
  <html>
    ...
  <body>
    ...
    ${page.content}
    ...
  </body>
  </html>
''
----

TIP: Layout templates are just functions, in case the normal template return the full page source like in a rss feed case, it is possible to set the page `layout` to the `lib.id` function. This will make the layout evaluation transparent and return the `template` result.


==== Normal templates

Normal templates are similar to the layout templates, with the only difference that their result will be evaluated by a layout template.
A standard normal template will set or update the `content` attribute of a page attribute set so the layout template can produce the final source.

Normal templates can also add other attributes to the page attribute to fit any special need.

[source, nix]
.Example of a normal template
----
{ templates, ... }:
page:
let
  content =
    ''
      <h1>${page.title}</h1>
    '';
in
  page // { inherit content; }
----

NOTE: In some cases, it is useful to have the normal template to return the final source of the page. +
By setting the page `layout` to the `lib.id` function, it is possible to bypass the layout template and have the normal template result being the final source source of the page.

==== Partial templates

Partials templates are templates that can be used in normal or layout templates.

Partial templates can take arguments and be used to with `mapTemplate` to apply a template to a list of content, or just be used as includes.

==== Template environment

The template environment is the first parameter to every template function.

It is automatically set when the templates are loaded from a theme.

The default template environment consists in:

- `conf`: The configuration attribute set.
- `lib`: The library attribute set, it contains Styx and nixpkgs library functions.
- `templates`: The templates attribute set.
- `data`: The data attribute set.
- `pages`: The pages attribute set.

NOTE: The template environment is set in `site.nix` and can be easily modified upon needs.

==== Template environment in templates

There are two ways of writing the template environment in the template, as a variable or as a deconstructed set.

[source, nix]
.Environment as a variable
----
env: # <1>
page:
''
  ${env.conf.theme.site.title}
''
----

<1> `env` is used as a set, and its key can be accessed with `.`.

[source, nix]
.Environment as a deconstructed set
----
{ conf, lib, ... }: # <1>
page: # <1>
''
  ${conf.site.theme.site.title}
''
----

<1> environment is deconstructed in its keys values. The last `...` means all others keys and is required if the set contains more keys than the keys deconstructed.


==== Calling templates in templates

It is possible to call templates in a template by accessing to the `templates` attribute of the template environment.

[source, nix]
.Calling a template in a template
----
{ templates, ... }:
page:
''
  ${templates.partials.head}
''
----

NOTE: When templates are loaded, they will automatically receive the template environment as a parameter, this will partially evaluate the template function. This means that the template function will be become a single argument function (`page`).

WARNING: Trying to call the current template will trigger an infinite loop and make the site generation fail.

==== Applying templates to multiple contents

The `mapTemplate` function can be used to map a template to a list of contents.

[source, nix]
.Applying a template to multiple contents
----
{ templates, ... }:
page:
''
  <ul>
  ${mapTemplate templates.post.list page.posts}
  </ul>
''
----


=== Combining Themes

In `site.nix`, themes are declared as a list. If multiple themes in the list provide the same file or template, the last theme in the list to declare it will be used.

This allows to "extends" themes without modifying them.

For example, to use the showcase theme but only customize the `layout` template. It is possible to create a new theme, and copy the `layout.nix` template in the new theme `templates` directory and change only this file.

[source, bash]
.Creating a new foo theme
----
$ styx new theme foo --in themes # <1>
$ cp $(nix-build -A styx-themes.showcase '<nixpkgs>')/templates/layout.nix themes/foo/templates/layout.nix # <2>
----

<1> Create the theme directory.
<2> Copying `showcase` theme `layout.nix` to the `foo` theme.

After a new theme has been created, it must be declared in `site.nix` to be used.

TIP: Combining themes is the recommended way to extend an existing theme as it make theme upgrades easier.

